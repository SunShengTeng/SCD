buildscript {
    project.ext {
        jvmVersion = '1.8'
        springBootVersion = '2.1.5.RELEASE'

        junitVersion = '4.12'
        // cloud:
        springCloudVersion = 'Greenwich.SR1' // Please use RELEASE in prod
        springCloudSleuthVersion = '2.1.1.RELEASE'
        springCloudConsulVersion = '2.1.1.RELEASE'
        springCloudEurekaVersion = '2.1.1.RELEASE'

        //grpc
        grpcVersion = '1.10.0'
        grpcSpringBootStarterVersion = '2.3.0.RELEASE'

    }
    // 仓库配置
    repositories {
        maven {
            url "https://maven.aliyun.com/nexus/content/groups/public/"
        }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}
allprojects {
    group 'cn.sunshengteng.scd'
    version '1.0.0-SNAPSHOT'

    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'maven'

    sourceCompatibility = 1.8

    repositories {
        maven {
            url "http://maven.aliyun.com/nexus/content/groups/public/"
        }
    }
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        sourceCompatibility = jvmVersion
        targetCompatibility = jvmVersion
    }
    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
        all {
            resolutionStrategy.cacheChangingModulesFor  0, 'seconds'
        }
    }
}
subprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
    dependencies {
        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
        compile 'org.springframework.boot:spring-boot-starter-web'

        //config
        /*compile 'org.springframework.cloud:spring-cloud-starter-config'*/
        //bus
        /*compile "org.springframework.cloud:spring-cloud-bus"
        compile "org.springframework.cloud:spring-cloud-starter-bus-amqp"*/

        compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop'

        // https://mvnrepository.com/artifact/org.projectlombok/lombok
        annotationProcessor group: 'org.projectlombok', name: 'lombok'

        testCompile 'org.springframework.boot:spring-boot-starter-test'
        testCompile "junit:junit:${junitVersion}"
    }
}
project(':eureka-server') {
    dependencies {
        compile "org.springframework.cloud:spring-cloud-starter-netflix-eureka-server"
    }
}
project(':config-server') {

}
project(':common-utils') {
    dependencies {
        // https://mvnrepository.com/artifact/com.alibaba/fastjson
        compile group: 'com.alibaba', name: 'fastjson', version: '1.2.47'
        compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.12'
        // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
        compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    }
    def name = 'common-utils'
    jar.enabled = true
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: 'file://' + new File(System.getProperty('user.home'), 'server/gradle-6.5.1/caches/modules-2/files-2.1').absolutePath)
                pom.version = "$project.version"
                pom.artifactId = "$name"
                pom.groupId = "$project.group"
            }
        }
    }

}

